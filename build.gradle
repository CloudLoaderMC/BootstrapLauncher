import ml.darubyminer360.gradleutils.GradleUtils

plugins {
    id 'ml.darubyminer360.gradleutils' version '2.2.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

group 'ml.cloudmc'

version = gradleutils.getTagOffsetVersion()
logger.lifecycle("Version: $version")

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'Cloud'
        url = 'https://maven.cloudmc.ml/releases/'
    }
    maven {
        name 'forge'
        url 'https://maven.minecraftforge.net/'
    }
}

dependencies {
    implementation('ml.cloudmc:SecureJarHandler:2.1.4')
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    withSourcesJar()
}

changelog {
    fromTag "0.1"
}

jar {
    //noinspection UnnecessaryQualifiedReference
    manifest.attributes(
            'Specification-Title': 'bootstraplauncher',
            'Specification-Vendor': 'forge',
            'Specification-Version': '1', // We are version 1 of ourselves
            'Implementation-Title': project.name,
            'Implementation-Version': "${project.version}+${System.getenv("BUILD_NUMBER") ?: 0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
            'Implementation-Vendor': 'forge',
            'Implementation-Timestamp': java.time.Instant.now().toString(),
            'Git-Commit': gradleutils.gitInfo.abbreviatedId,
            'Git-Branch': gradleutils.gitInfo.branch,
            'Build-Number': "${System.getenv("BUILD_NUMBER") ?: 0}",
            'Main-Class': 'cpw.mods.bootstraplauncher.BootstrapLauncher'
    )
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        pom {
            name = 'Bootstrap launcher'
            description = 'Allows bootstrapping a modularized environment from a classpath one'
            url = 'https://github.com/CloudLoaderMC/BootstrapLauncher'
            scm {
                url = 'https://github.com/CloudLoaderMC/BootstrapLauncher'
                connection = 'scm:git:git@github.com:CloudLoaderMC/BootstrapLauncher.git'
                developerConnection = 'scm:git:git@github.com:CloudLoaderMC/BootstrapLauncher.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/CloudLoaderMC/BootstrapLauncher/issues'
            }
            developers {
                developer {
                    id = 'DaRubyMiner360'
                    name = 'DaRubyMiner360'
                }
            }
        }
    }
    repositories {
        maven GradleUtils.getPublishingCloudMaven(project)
    }
}


